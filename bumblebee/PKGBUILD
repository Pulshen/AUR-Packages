# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=bumblebee
pkgver=3.2.1
pkgrel=13
pkgdesc="NVIDIA Optimus support for Linux through VirtualGL"
arch=('i686' 'x86_64')
depends=('virtualgl' 'glib2' 'mesa-libgl')
makedepends=('help2man')
optdepends=('bbswitch: switch on/off discrete card'
            'nvidia: NVIDIA kernel driver'
            'nvidia-340xx: NVIDIA kernel driver for older devices'
            'nvidia-304xx: NVIDIA kernel driver for even older devices'
            'primus: faster back-end for optirun')
if [ "$CARCH" = "x86_64" ]; then
     optdepends[${#optdepends[@]}]='lib32-virtualgl: run 32bit applications with optirun'
     optdepends[${#optdepends[@]}]='lib32-primus: faster back-end for optirun'
fi
conflicts=('nvidia-libgl')
provides=('nvidia-libgl')
url="http://www.bumblebee-project.org"
license=("GPL3")
install='bumblebee.install'
backup=('etc/bumblebee/bumblebee.conf'
        'etc/bumblebee/xorg.conf.nouveau'
        'etc/bumblebee/xorg.conf.nvidia')
source=("http://www.bumblebee-project.org/${pkgname}-${pkgver}.tar.gz"
        "0001-bb_nvidia_modeset-detection_bug699_01.patch::https://github.com/arafey/Bumblebee/commit/5636b24fa86a005a5d2e30bd794516db13ccba56.patch"
        "0002-bb_nvidia_modeset-detection_bug699_02.patch::https://github.com/arafey/Bumblebee/commit/09d537e8e5313cd0f2c7bf6620ca70454de8a04a.patch"
        "0003-bb_nvidia_umv_detection_bug699.patch::https://github.com/arafey/Bumblebee/commit/dbbf20a38aa2bffb10c4e8af583b34dff6bfe721.patch"
        "0004-bb_nvidia_drm_detection_bug699_01.patch::https://github.com/arafey/Bumblebee/commit/bcfe4dd16dd6194f1edbdc53b874a4f408343c5c.patch"
        "0005-bb_nvidia_drm_detection_bug699_02.patch::https://github.com/arafey/Bumblebee/commit/fcfe596eb13f62ca9dd7de272a5a87ae843b2a00.patch"
        "0006-bb_hexadicimal_bug573.patch::https://github.com/Bumblebee-Project/Bumblebee/commit/2073f8537412aa47755eb6f3f22a114403e5285b.patch"
        "0007-bb_mutebblogger.patch"
        "0008-nvidia-340xx.patch::https://github.com/Sudokamikaze/Bumblebee/commit/72e2f662664b94131ca6df9aeae841aa5681a4e9.patch"
        "0009-nvidia-340xx_02.patch::https://github.com/Sudokamikaze/Bumblebee/commit/518c63b02ba604b0f5bbc4ce3383962044f33b28.patch"
        "0010-fix.patch::https://github.com/Sudokamikaze/Bumblebee/commit/3f2dda849ebeb6385a1d375bf26a1db096e4c3fb.patch"
        "bumblebee.conf")
sha256sums=('1018703b07e2f607a4641249d69478ce076ae5a1e9dd6cff5694d394fa7ee30e'
            'aff3528d17a77ff19b4e0a7a10682b8351456f11795f71ef62b315e774fb408a'
            '70ad9b3d8e0d70a504110651c6f5f3a1b1d3c4c44eeb0fd49a4463e99124a47b'
            '16fd522f412125b3c9b5709d78584744c70cb627e8baf8cd6025a71d278f79a6'
            '4676606012319ce7a9e3890d6e83445ae674f5ff2994925d59ec2229fbae5a14'
            '206f71c022b29a77227a3c7ba1f5cf68e219b957b5d807be9a30b6a6a7cafddc'
            '0b7c1f4bb2e27d131c6c21fd7006d075584917ac4259bd9899e6eca99efc0ece'
            'cbe3e1717bc80146b87d8f2ab1158ee9e094ea5bb2ca9a4a8c09c24b086a7792'
            '67fd9d681ad47fe78b11cb351e0144b69865d5d9b431d7a81abc08497952920e'
            '34a1b39c49dd97dae6d11a42a63ac74d56f9800ef9254eaf62b422bb79e38899'
            'e9016d442595d613c4f56aa09492c1a825a6aaa06278ffe1ca6c142660ab941f'
            'aa3750984869a57126e557f08c53b97ba1161a479e504ce8be7ef7b2b6a519dd')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    for p in ${srcdir}/*.patch; do
        patch -Np1 -i "$p"
    done

    ./configure \
        CONF_DRIVER_MODULE_NVIDIA=nvidia \
        CONF_LDPATH_NVIDIA=/usr/lib/nvidia:/usr/lib32/nvidia \
        CONF_MODPATH_NVIDIA=/usr/lib/nvidia/xorg/,/usr/lib/xorg/modules \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-udev-rules=/usr/lib/udev/rules.d/ \
        --sysconfdir=/etc \
        --without-pidfile
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Install main app
    make install DESTDIR="$pkgdir" \
      completiondir=/usr/share/bash-completion/completions

    # Blacklist nvidia and nouveau modules
    # Reference: https://github.com/Bumblebee-Project/Bumblebee/issues/719
    install -Dm644 "${srcdir}/bumblebee.conf" "${pkgdir}/usr/lib/modprobe.d/bumblebee.conf"

    # Install systemd unit
    install -Dm644 "scripts/systemd/bumblebeed.service" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"
    sed -i "s/sbin/bin/" "${pkgdir}/usr/lib/systemd/system/bumblebeed.service"

    # Make bash_completion work
    mv -v "$pkgdir"/usr/share/bash-completion/completions/{bumblebee,optirun}
}
