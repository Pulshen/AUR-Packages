# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>
# Contributor: Pulshen Sudokamikaze <github.com/Sudokamikaze>

pkgname=lib32-libnm-glib46
pkgver=1.3.91
pkgrel=2
pkgdesc='NetworkManager library (32-bit)'
arch=('x86_64')
url='http://www.gnome.org/projects/NetworkManager/'
license=('GPL2' 'LGPL2.1')
depends=('lib32-dbus-glib' 'lib32-glib2' 'lib32-glibc' 'lib32-libdbus'
         'lib32-libgudev' 'lib32-nspr' 'lib32-nss' 'lib32-util-linux')
makedepends=('dhcpcd' 'dhclient' 'dhcp-client' 'gtk-doc' 'intltool' 'iproute2'
             'iptables' 'lib32-libmm-glib' 'lib32-libndp' 'lib32-libnewt'
             'lib32-libnl' 'lib32-polkit' 'lib32-libsoup' 'lib32-libteam'
             'modemmanager' 'ppp' 'python2' 'vala' 'wpa_supplicant')
replaces=('lib32-libnm-glib')
source=("http://ftp.gnome.org/pub/gnome/sources/NetworkManager/${pkgver%.*}/NetworkManager-${pkgver}.tar.xz"
        'disable_set_hostname.patch')
sha256sums=('c0c02a21827661a2bef70711ec8e5b8b83880b5cd59cbd367444b9ea8b7d86b8'
            '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460')

prepare() {
  cd NetworkManager-$pkgver

  patch -Np1 -i ../disable_set_hostname.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd NetworkManager-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/networkmanager' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --enable-modify-system \
    --disable-doc \
    --disable-introspection \
    --disable-more-warnings \
    --disable-qt \
    --disable-static \
    --disable-wimax \
    --with-crypto='nss' \
    --with-dhclient='/usr/bin/dhclient' \
    --with-dhcpcd='/usr/bin/dhcpcd' \
    --with-dnsmasq='/usr/bin/dnsmasq' \
    --with-iptables='/usr/bin/iptables' \
    --with-modem-manager-1 \
    --with-resolvconf='/usr/bin/resolvconf' \
    --with-systemdsystemunitdir='/usr/lib/systemd/system' \
    --with-udev-dir='/usr/lib32/udev' \
    --with-session-tracking='no'
  make -C libnm-util
  make -C libnm-glib
}

package() {
  cd NetworkManager-$pkgver

  make DESTDIR="${pkgdir}" -C libnm-util install
  make DESTDIR="${pkgdir}" -C libnm-glib install
  rm -rf "${pkgdir}"/usr/{include,lib32/pkgconfig,share}
  find "${pkgdir}" -type l -delete
}

# vim: ts=2 sw=2 et:
